selectClass
{
	selector
	{
		condition team == TEAM_ALIENS
		{
			spawnAs PCL_ALIEN_BUILDER0
		}
		condition team == TEAM_HUMANS
		{
			spawnAs WP_HBUILD
		}
	}
}

selector
{
	condition ( team == TEAM_HUMANS )
	{
		selector
		{
			behavior unstick
			behavior use_medkit

			sequence
			{
				condition !haveWeapon( WP_HBUILD )
				selector
				{
					action buy( WP_HBUILD )
					action suicide
				}
			}

			condition ( distanceTo( E_USERPOS ) > 700 )
			{
				action moveTo( E_USERPOS )
			}

			condition ( distanceTo( E_USERPOS ) < 700 )
			{
				// make the bot stand still if this fails
				decorator invert()
				{
					selector
					{
						action buildNowChosenBuildable
						action repair
					}
				}
			}

			action roamInRadius( E_USERPOS, 700 )
			decorator timer( 3000 )
			{
				action roam
			}

		}
	}
	condition ( team == TEAM_ALIENS )
	{
		selector
		{
			behavior unstick

			sequence
			{
				condition class != PCL_ALIEN_BUILDER0 && class != PCL_ALIEN_BUILDER0_UPG
				selector
				{
					sequence
					{
						action moveTo( E_A_OVERMIND )
						selector
						{
							action evolveTo( PCL_ALIEN_BUILDER0_UPG )
							action evolveTo( PCL_ALIEN_BUILDER0 )
						}
					}
					action suicide
				}
			}

			condition ( distanceTo( E_USERPOS ) > 700 )
			{
				action moveTo( E_USERPOS )
			}

			condition ( distanceTo( E_USERPOS ) < 700 )
			{
				// make the bot stand still if this fails
				decorator invert()
				{
					action buildNowChosenBuildable
				}
			}

			action roamInRadius( E_USERPOS, 700 )
			decorator timer( 3000 )
			{
				action roam
			}
		}
	}
}
